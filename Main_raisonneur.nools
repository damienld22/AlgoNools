//	*******************************************************************
//
//	Fichier comportant toutes les règles destinées au raisonneur
//
//	*******************************************************************


import("./Functions.js");
import("./Objets.js");



//	===================================================================
//	Règles pour gérer la luminosité
//		->	augLum1 et augLum2 pour l'augmenter
//		->	dimLum1 et dimLum2 pour diminuer
//	===================================================================
rule augLum1 {
	when {
		consigne: Consigne;
		capLum  : CaptLuminosite capLum.getPlace() == 'interieur' && capLum.getValue() < (consigne.lum - 25);
		volet   : Volet volet.getValue() == 100;
		lum     : Lumiere lum.getValue() < 100;		
	}
	then {
		modify(lum, lum.setUp());
	}
}
rule augLum2 {
	when {
		consigne : Consigne;
		capLum   : CaptLuminosite capLum.getPlace() == 'interieur' && capLum.getValue() < (consigne.lum - 25);
		volet    : Volet volet.getValue() < 100;
		capLumExt: CaptLuminosite capLumExt.getPlace() == 'exterieur' && capLumExt.getValue() > consigne.jourSuf;
	}
	then {
		modify(volet, volet.setUp());
	}
}
rule dimLum1 {
	when {
		consigne: Consigne;
		capLum  : CaptLuminosite capLum.getPlace() == 'interieur' && capLum.getValue() > (consigne.lum + 25);
		lum     : Lumiere lum.getValue() > 0;
	}
	then {
		modify(lum, lum.setDown());
	}
}
rule dimLum2 {
	when {
		consigne: Consigne;
		capLum  : CaptLuminosite capLum.getPlace() == 'interieur' && capLum.getValue() > (consigne.lum + 25);
		lum     : Lumiere lum.getValue() == 0;
		volet   : Volet volet.getValue() > 0;
	}
	then {
		modify(volet, volet.setDown());
	}
}


//	==================================================================
//	Règles pour gérer le chauffage
//		->	augChauf1, augChauf2 et augChauf3 pour l'augmenter
//		->	dimChauf pour le diminuer
//	==================================================================
rule augChauf1 {
	when {
		consigne  : Consigne;
		capTemp   : CaptTemperature capTemp.getPlace() == 'interieur' && capTemp.getValue() < (consigne.temp - 1);
		volet     : Volet volet.getValue() < 100;
		capTempExt: CaptTemperature capTempExt.getPlace() == 'exterieur' && capTempExt.getValue() >= consigne.tempSuf;
		capLum    : CaptLuminosite capLum.getPlace() == 'exterieur' && capLum.getValue() >= consigne.lumSuf;
	}
	then {
		modify(volet, volet.setUp());
	}
}
rule augChauf2 {
	when {
		consigne: Consigne;
		chauf   : Chauffage;
		capTemp : CaptTemperature capTemp.getPlace() == 'interieur' && capTemp.getValue() < (consigne.temp - 1);
		volet   : Volet volet.getValue() < 100;
		or( capTempExt: CaptTemperature capTempExt.getPlace() == 'exterieur' && capTempExt.getValue() < consigne.tempSuf,
			capLum    : CaptLuminosite capLum.getPlace() == 'exterieur' && capLum.getValue() < consigne.lumSuf);
	} 
	then {
		modify(chauf, chauf.setUp());
	}
}
rule augChauf3 {
	when {
		consigne: Consigne;
		capTemp : CaptTemperature capTemp.getPlace() == 'interieur' && capTemp.getValue() < (consigne.temp - 1);
		volet   : Volet volet.getValue() == 100;
		chauf   : Chauffage;
	}
	then {
		modify(chauf, chauf.setUp());
	}
}
rule dimChauf {
	when {
		consigne: Consigne;
		chauf   : Chauffage;
		capTemp : CaptTemperature capTemp.getPlace() == 'interieur' && capTemp.getValue() > (consigne.temp + 1);
	}
	then {
		modify(chauf, chauf.setDown());
	}
}